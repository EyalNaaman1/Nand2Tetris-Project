// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    /** Initializes the keyboard. */
    function void init() {
        return;
    } 

    function char keyPressed() {
        // Uses Memory.peek
        return Memory.peek(24576);
    }

    /**								
        * Waits until a key is pressed on the keyboard and released,
        * then echoes the key to the screen, and returns the character 
        * of the pressed key.
        */
    function char readChar() {
        // This should behave exactly like the built-in OS.
        // Pseudocode:
        // 1. display the cursor
        // 2. while (keyPressed() = 0): do nothing
        // 3. let c = code of the currently pressed key
        // 4. while (~(keyPressed() = 0)): do nothing
        // 5. display c at the current cursor location
        // 6. advance the cursor
        // 7. return c
        var char c;
        do Output.printChar(95); // display the cursor '_'
        while (Keyboard.keyPressed() = 0) {
            // do nothing
        }
        let c = Keyboard.keyPressed();
        while (~(Keyboard.keyPressed() = 0)) {
            // do nothing
        }
        do Output.backSpace();
        do Output.printChar(c);

        return c;
    }

    /**								
        * Displays the message on the screen, reads from the keyboard the entered
        * text until a newline character is detected, echoes the text to the screen,
        * and returns its value. Also handles user backspaces if the current value
        * is longer than a single character.
        */
    function String readLine(String message) {
        // This should behave exactly like the built-in OS.
        // You can assume input is at most 64 characters long.
        // Why? Because this is the width of our screen!
        // Pseudocode:
        // 1. printString(message)
        // 2. let str = ""
        // 3. while true
        // 4.   let c = readChar()
        // 5.   if (c = newLine)
        // 6.      display newLine (if not displayed already by readChar())
        // 7.      return str
        // 8.   else if (c = backSpace)
        // 9.      remove the last character from str, if possible
        // 10.     move the cursor accordingly
        // 11.  else
        // 12.    str.appendChar(c)
        var String str;
        var char c;
        do Output.printString(message);
        let str = String.new(64);
        while (true) {
            let c = Keyboard.readChar();
            if (c = String.newLine()) {
                do Output.println();
                return str;
            }  
            if (c = String.backSpace()) {
                if (str.length() > 0) {
                do str.eraseLastChar();
                }
            } else {
                do str.appendChar(c);
            }
        }
        return str;
    }     

    /**								
        * Displays the message on the screen, reads from the keyboard the entered
        * text until a newline character is detected, echoes the text to the screen,
        * and returns its integer value (until the first non-digit character in the
        * entered text is detected). Also handles user backspaces. 
        */
    function int readInt(String message) {
        // This should behave exactly like the built-in OS.
        var String str;
        var char c;
        var int val;
        do Output.printString(message);
        let str = String.new(64);
        while (true) {
            let c = Keyboard.readChar();
            if (c = String.newLine()) {
                let val = str.intValue();
                do str.dispose();
                return val;
            }
            if (c = String.backSpace()) {
                if (str.length() > 0) {
                do str.eraseLastChar();
                }
            } 
            else {
                if (((c > 47) & (c < 58)) | ((c = 45) & (str.length() = 0))) {
                do str.appendChar(c);
            } else {
                let val = str.intValue();
                do str.dispose();
                return val;
                }
            }
        }
        return val;
    }
}