// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array powersOfTwo;
    static int accumulatedY;
    /** Initializes the library. */
    function void init() {
        let powersOfTwo = Array.new(16);
        let powersOfTwo[0] = 1;
        let powersOfTwo[1] = 2;
        let powersOfTwo[2] = 4;
        let powersOfTwo[3] = 8;
        let powersOfTwo[4] = 16;
        let powersOfTwo[5] = 32;
        let powersOfTwo[6] = 64;
        let powersOfTwo[7] = 128;
        let powersOfTwo[8] = 256;
        let powersOfTwo[9] = 512;
        let powersOfTwo[10] = 1024;
        let powersOfTwo[11] = 2048;
        let powersOfTwo[12] = 4096;
        let powersOfTwo[13] = 8192;
        let powersOfTwo[14] = 16384;
        let powersOfTwo[15] = 16384 + 16384;
        return;
    }   

    function boolean bit(int x, int i) {
    return ~((x & powersOfTwo[i]) = 0);
    }

    function int getPowerOfTwo(int i) {
    return powersOfTwo[i];
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            return -x;
        }
        return x;
    }

    /** 
        * Returns the product of x and y. 
        * When a Jack compiler detects the multiplication operator '*' in the 
        * program's code, it handles it by invoking this method. In other words,
        * the Jack expressions x*y and multiply(x,y) return the same value.
        */
    function int multiply(int x, int y) {
        // Pseudocode:
        // 1. let sum = 0
        // 2. let shiftedx = x
        // 3. for i = 0 ... n-1 do
        // 4.   if ((i-th bit of y) == 1)
        // 5.      let sum = sum + shiftedx
        // 6.   let shiftedx = 2*shiftedx
        // 7. return sum
        var int sum, shiftedX, i;
        let sum = 0;
        let shiftedX = x;
        let i = 0;
        while (i < 16) {
            if (Math.bit(y, i)) {
                let sum = sum + shiftedX;
            }
            let shiftedX = shiftedX + shiftedX;
            let i = i + 1;
        }
        return sum;
    }

    /** 
        * Returns the integer part of x/y.
        * When a Jack compiler detects the multiplication operator '/' in the 
        * program's code, it handles it by invoking this method. In other words,
        * the Jack expressions x/y and divide(x,y) return the same value.
        */
    function int divide(int x, int y) {
        // This should be implemented without using multiplication.
        // Hint: updating the value of 2*q*y in the "if" statement requires
        // at most a single addition operator at every recursion step.
        // Pseudocode:
        // 1. if (y > x) return 0
        // 2. let q = divide(x, 2*y)
        // 3. if ((x - 2*q*y) < y)
        // 4.   return 2*q
        // 5. else
        // 6.   return 2*q + 1
        var int absX, absY, result;
        var boolean isNegative;

        let isNegative = ((x < 0) & (y > 0)) | ((x > 0) & (y < 0));
        let absX = Math.abs(x);
        let absY = Math.abs(y);

        let accumulatedY = 0;
        let result = Math.divideHelper(absX, absY);
        if (isNegative) {
            return -result;
        }
        return result;
    }

    function int divideHelper(int x, int y) {
        var int q;
        if ((y > x) | (y < 0)) {
            return 0;
        }
        let q = Math.divideHelper(x, y + y);
        if ((x - accumulatedY) < y) {
            return q + q;
        } else {
            let accumulatedY = accumulatedY + y;
            return q + q + 1;
        }
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        // This should be implemented without using multiplication or division.
        // Pseudocode (where '**' is the mathematical power function):
        // 1. let y = 0
        // 2. for j = (n/2 - 1) ... 0 do
        // 3.   if ((y + 2**j)**2 <= x) then let y = y + 2**j
        // 4. return y
        var int y, j, approx, approxSq;
        if (x < 0) {
            do Sys.error(4);
            return 0;
        }
        let y = 0;
        let j = 7;
        while (j > -1) {
            let approx = y + powersOfTwo[j];
            let approxSq = Math.multiply(approx, approx);
            if (~(approxSq > x) & (approxSq > 0)) {
                let y = approx;
            }
            let j = j - 1;
        }
        return y;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a > b) {
            return a;
        }
        return b;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        }
        return b;
    }
}