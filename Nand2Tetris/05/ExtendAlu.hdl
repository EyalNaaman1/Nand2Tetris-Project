// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).


// The ExtendAlu chip is an extension of the standard ALU which also supports
// shift operations.
// The inputs of the extended ALU are instruction[9], x[16], y[16].
// The "ng" and "zr" output pins behave the same as in the regular ALU.
// The "out" output is defined as follows:
// If instruction[8]=1 and instruction[7]=1 the output is identical to the 
// regular ALU, where:
// instruction[5]=zx, instruction[4]=nx, ..., instruction[0]=no
// Else, if instruction[8]=0 and instruction[7]=1, the output is a shift:
// - If instruction[4] == 0, the input "y" will be shifted, otherwise "x".
// - If instruction[5] == 0, the shift will be a right-shift, otherwise left.
// - All other inputs are undefined.

CHIP ExtendAlu {
     IN x[16], y[16], instruction[9];
     OUT out[16], zr, ng;
     
     PARTS:
     ALU(x=x, y=y,
         zx=instruction[5], nx=instruction[4],
         zy=instruction[3], ny=instruction[2],
         f =instruction[1], no=instruction[0],
         out=outALU, zr=zrDummy, ng=ngDummy);

     // shift logic
     Mux16(a=y, b=x, sel=instruction[4], out=shiftIn);

     ShiftLeft(in=shiftIn,out=outShiftLeft);
     ShiftRight(in=shiftIn, out=outShiftRight);
     Mux16(a=outShiftRight, b=outShiftLeft, sel=instruction[5], out=outShift);

     //choose output: ALU or Shift
     And(a=instruction[7], b=instruction[8], out=isALU);
     Not(in=instruction[8], out=notInstr8);
     And(a=notInstr8, b=instruction[7], out=isShift);
     Or(a=isALU, b=isShift, out=doSomething);
     Mux16(a=outShift, b=outALU, sel=isALU, out=goodOut);
     //final output
     Mux16(a=false, b=goodOut, sel=doSomething, out=out1, out=out);

     //final zr and ng
     ALU(x=out1, y=false, zx=false, nx=false, zy=true, ny=true, f=false, no=false, out=outDummy, zr=zr, ng=ng);
}